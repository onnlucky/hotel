parser = parser {
     end: !_
      sp: " " | "\t"
      nl: "\r\n" | "\n\r" | "\n" | "\r"

slcomment: "#" (!nl _)* (nl|end)
 icomment: "(#" (!"#)" (icomment|_))* ("#)"|end)
  comment: (slcomment | icomment)

      ws: (sp | icomment)*
    wsnl: (sp | nl | comment)*

     eos: . (";" | nl | slcomment)
     eov: . ("," | nl | slcomment)
   eostm: . (";" | nl | end | ")" | "}" | "]" | slcomment)
  eoexpr: . ("," | ";" | nl | end | ")" | "}" | "]" | slcomment)

  letter: [@abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ_0123456789]
   first: [@abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ_]
    name: f=first fs=letter* -> String(f,fs)

// ---- TEXT ----

  escape: "\\\""      -> '"'
        | "\\n"       -> '\n'
        | "\\r"       -> '\r'
        | "\\t"       -> '\t'
        | "\\\\"      -> '\\'
        | !"\"" _
   stext: "\"" ts=escape* @"a closing '\"'" "\"" -> ts.join
    text: stext

// ---- NUMBERS ----

 numeric: [0123456789]
    sign: "-"  -> -1
        | "+"  -> 1
        | ""   -> 1
    numr: n=numeric "_" b=numr  -> b.prepend(n - 48)
        | n=numeric b=numr      -> b.prepend(n - 48)
        | n=numeric             -> [n - 48]
     num: s=sign n=numr         -> s * n.reduce(l, r -> l * 10 + r)

// ---- OBJECT, MAP AND LIST ----

  object: "{".. is=items @"a closing '}'" .."}"   -> {type:"object",data:is}
        | "{"..@"a closing '}'" "}"
     map: "[".. is=items @"a closing ']'" .."]"   -> {type:"map",data:is}
        | "["..":" @"a closing ']'" .."]"
   items: i=item is=(eov.. items)*                -> is.prepend(i)
    item: n=name .@"a ':'" ":"..@"a value" v=expr -> {{n, v}}

    list: "[".. is=litems @"a closing ']'" .."]"  -> {type:"list",data:is}
        | "[".."]"
  litems: i=expr !":" is=(eov.. litems)*          -> is.prepend(i)

// ---- FUNCTION DEFINITION ----

   class: "{".. as=fargs .."->"..@"a closing '}'" b=body .."}" -> {{as, b}, type:"class"}
function: "(".. as=fargs .."->"..@"a closing ')'" b=body ..")" -> {{as, b}, type:"function"}
        | as=fargs .."->".. b=bodynl                           -> {{as, b}, type:"function"}

   fargs: a=name as=(..","..@"a argument name after a ','" name)* -> as.prepend(a)
        | ""

// ---- EXPRESSIONS ----

   value: function
        | class
        | object
        | "true" | "false" | "null" | "undefined" | num | text | list | map
        | n=name -> {type:"ref",n:n}

    args: e=expr es=(..","..@"a value after ','" expr)* -> es.prepend(e)
        | ""

     met: "." | "::" | "?" | "!"
    tail: ."("..@"a closing ')'" as=args ..")" t=tail                 -> {{as}, tail:t, type:"call"}
        | .m=met.. n=name "("..@"a closing ')'" as=args ..")" t=tail  -> {{m,n,as}, tail:t, type:"method"}
        | .m=met..@"a method call" n=name t=tail                      -> {{m,n}, tail:t, type:"method"}
        | ""                                                          -> {type:"end"}
    call: v=value t=tail

      op: "+" | "-" | "*" | "/" | "%" | "^"
    expr: l=call (..op=op.. r=expr)*

// ---- STATEMENTS ----

     stm: n=name .":"!":". @"a value" e=expr -> { type:"assign", n: n, e: e }
        | expr

    stms: t=stm ts=(.";".@"statement after ';'" stm)* -> ts.prepend(t)
    // stms: stm+(.";".@"statement after ';'")

// ---- BLOCKS ----

    body: ts=stms tss=(eos.. stms)* -> tss.prepend(ts).flatten
        | ""

  bodynl: ts=stm

hashbang: "#!" (!nl _)*
   start: .. hashbang? .. b=body .. end -> b
}

