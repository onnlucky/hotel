set_target = { call, fn ->
    if not call: return fn
    if not call.target: return _Map_set(call, #target, fn)
    _Map_set(call, #target, set_target(call.target, fn))
}
add_block = { call, block ->
    if not block: return call
    _Map_set(call, #block, block)
}
nvalue = { name ->
    if name == "null": return false
    if name == "undefined": return false
    if name == "true": return false
    if name == "false": return false
    return true
}
nreserved = { name ->
    if name == "not": return false
    if name == "and": return false
    if name == "or": return false
    return nvalue(name)
}

var $current_indent = [0]
begin_body = { indent ->
    if indent <= $current_indent.get(0): return false
    $current_indent = $current_indent.prepend(indent)
    return true
}
in_body = { indent, i ->
    return indent <= i
}
end_body = { indent ->
    $current_indent = $current_indent.slice(1)
    return true
}

parse = parser {
      sp: " " | "\t"
      nl: "\r\n" | "\n\r" | "\n" | "\r"

slcomment: "//" (!nl _)*
 icomment: "/*" (!"*/" _)* ("*/"|end)
  comment: (slcomment (nl|end) | icomment)

      ws: (sp | icomment)*
    wsnl: (sp | nl | comment)*

     eos: . (";" | nl) wsnl
     eov: . ("," | nl) wsnl
   eostm: . (";" | nl | end | ")" | "}" | "]" | slcomment)
  eoexpr: . ("," | ";" | nl | end | ")" | "}" | "]" | slcomment)

   punct: [ \t\n\r.!:;,=(){}[\]"']
nonident: punct | "/" | "\\" | "<" | ">"
   first: [_abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ]
    name: n=first rs=(!nonident _)*  -> rs.prepend(n).toChar
   ident: n=name ?(nvalue n) -> n.toSym
reference: n=name ?(nreserved n) -> n.toSym

 opchars: rs=(!first !punct !"//" _)+ -> rs.toChar
      op: "or"                        -> #or
        | "and"                       -> #and
        | n=opchars                   -> n.toSym

// ---- TEXT ----

  escape: "\\\""      -> "\""
        | "\\n"       -> "\n"
        | "\\r"       -> "\r"
        | "\\t"       -> "\t"
        | "\\\\"      -> "\\"
        | !"\"" l=_   -> l.toChar
   stext: "\"" ts=escape* @"a closing '\"'" "\"" -> ts.join

   embed: !stext e=paren                  -> { type: #call, target: #_Text_ToText, args: [e] }
    text: l=stext (rss=(.e=embed .t=stext -> [e, t]
                       |.e=embed          -> [e]
                       |.t=stext          -> [t]
                       )+                 -> { type: #call, target: #_Text_Cat, args: rss.reduce({l, r -> l.cat(r)}).prepend(l) }
                  | pass                  -> l
                  )

// ---- NUMBERS ----

    sign: "-"  -> -1
        | "+"  ->  1
        | pass ->  1
     num: s=sign n=numr        -> s * n.reduce({l, r -> l * 10 + r})
    numr: n=numeric "_" b=numr -> b.prepend(n - 48)
        | n=numeric b=numr     -> b.prepend(n - 48)
        | n=numeric            -> [n - 48]

// ---- OBJECT, MAP AND LIST ----

  object: "{" wsnl is=items wsnl @"a closing '}'" "}" -> { type: #object, items: is }
        | "{" wsnl @"a closing '}'" "}"               -> { type: #object, items: [] }
     map: "[" wsnl is=items wsnl @"a closing ']'" "]" -> { type: #map, items: is }
        | "[" wsnl ":" wsnl @"a closing ']'" "]"      -> { type: #map, items: [] }
   items: i=item eov wsnl is=items                    -> is.prepend(i)
        | i=item                                      -> [i]
    item: n=name .@"a ':'" ":" wsnl @"a value" v=expr -> { key: n, value: v }

    list: "[" wsnl is=litems @"a closing ']'" wsnl "]" -> { type: #list, items: is }
        | "[" wsnl @"a closing ']'" "]"                -> { type: #list, items: [] }
  litems: i=expr eov wsnl is=litems                    -> is.prepend(i)
        | i=expr                                       -> [i]

// ---- PARSER ----

    this: "@" n=ident -> { type: #self, name: n }
  varref: "\$" n=ident -> { type: #varref, name: n }
  symbol: "#" n=name  -> {type: #sym, name: n }

   value: function
        | ident
        | this | varref
        | num | symbol | text
        | list | object | map
        | "true"                                   -> true
        | "false"                                  -> false
        | "null"                                   -> { type: #value, value: #null }
        | "undefined"                              -> { type: #value, value: #undefined }

// TODO this is a bit cludgy ...
    expr: "!"!"=" b=bodynl                         -> { type: #call, target: #vm_task, args: [b] }
        | oplog

   oplog: l=opnot .("or"  wsnl r=oplog       -> { type: #call, target: #or,  args: [l, r] }
                   |"and" wsnl r=oplog       -> { type: #call, target: #and, args: [l, r] }
                   | pass                    -> l
                   )
   opnot: "not" wsnl r=opcmp                 -> { type: #call, target: #not, args: [r] }
        | opcmp
   opcmp: l=opadd .("<=" wsnl r=opcmp        -> { type: #call, target: #lte, args: [l, r] }
                   |"<"  wsnl r=opcmp        -> { type: #call, target: #lt,  args: [l, r] }
                   |">=" wsnl r=opcmp        -> { type: #call, target: #gte, args: [l, r] }
                   |">"  wsnl r=opcmp        -> { type: #call, target: #gt,  args: [l, r] }
                   |"==" wsnl r=opcmp        -> { type: #call, target: #eq,  args: [l, r] }
                   |"!=" wsnl r=opcmp        -> { type: #call, target: #neq, args: [l, r] }
                   | pass                    -> l
                   )
   opadd: l=opmul .("+" wsnl r=opadd         -> { type: #call, target: #add, args: [l, r] }
                   |"-" wsnl r=opadd         -> { type: #call, target: #sub, args: [l, r] }
                   | pass                    -> l
                   )
   opmul: l=oppow .("*" wsnl r=opmul         -> { type: #call, target: #mul, args: [l, r] }
                   |"/" wsnl r=opmul         -> { type: #call, target: #div, args: [l, r] }
                   |"%" wsnl r=opmul         -> { type: #call, target: #mod, args: [l, r] }
                   | pass                    -> l
                   )
   oppow: l=paren .("^" wsnl r=oppow         -> { type: #call, target: #pow, args: [l, r] }
                   | pass                    -> l
                   )

        // the first is an optimization of the second
   paren: "(".. c=stm ..")"                  -> bool(c.size == 1, c.get(0), c)
        | "(".. @"closing ')'" b=body ..")"  -> b
        | call

    args: a=expr as=(eov expr)*             -> as.prepend(a)
        | pass                              -> []

        // the ws is in front, because the empty tail should not eat whitespace, which might be significant
    tail: ."(".. @"a closing ')'" as=args ..")" t=tail                 -> set_target(t, { type: #call, target: null, args: as })
        | ."[".. l=expr? ..":".. @"a closing ']'" r=expr? .."]" t=tail -> set_target(t, { type: #send, target: null, msg: #slice, args: [l, r] })
        | ."[".. @"a closing ']'" e=expr .."]" t=tail                  -> set_target(t, { type: #send, target: null, msg: #get, args: [e] })
        | .".". n=ident ."(".. @"a closing ')'" as=args ..")" t=tail    -> set_target(t, { type: #send, target: null, msg: n, args: as })
        | .".". @"a message" n=ident t=tail                             -> set_target(t, { type: #send, target: null, msg: n, args: [], topcall: true })
        | pass                                                          -> null
    call: fn=value t=tail                                              -> set_target(t, fn)

 topargs: a=expr as=(.","@"arg".. expr)* -> as.prepend(a)
 topcall: fn=reference sp .!"(" (
              a=topcall                     -> { type: #call, target: fn, args: [a] }
            | as=topargs                    -> { type: #call, target: fn, args: as  }
        )
        // certain calls can be used as topcall, e.g. print.call debug.inspect 42
        | &reference fn=call ?(fn?topcall) sp .!"(" (
              a=topcall                     -> { type: #call, target: fn, args: [a] }
            | as=topargs                    -> { type: #call, target: fn, args: as  }
        )

function: "{".. as=fargs .."->"..@"a closing '}'"
                b=bodyempty
          .."}"                                   -> { type: #fn, args: as, body: b }
        | "{".. as=fargs .."=>"..@"a closing '}'"
                b=bodyempty
          .."}"                                   -> { type: #block, args: as, body: b }
        | as=fargs ."->".. b=bodynl               -> { type: #fn, args: as, body: b }
        | as=fargs ."=>".. b=bodynl               -> { type: #block, args: as, body: b }


   fargs: a=farg wsnl "," wsnl as=fargs        -> as.prepend(a)
        | a=farg                               -> [a]
        | pass                                 -> []
    farg: "&&" n=ident                         -> { type: #collectlazy, name: n }
        | "**" n=ident                         -> { type: #collecteager, name: n }
        | "&" n=ident                          -> { type: #lazy, name: n }
        | n=ident                              -> { type: #eager, name: n }

selfcall: fn=reference                         -> { type: #call, target: fn, args: [] }

     rhs: e=topcall .b=block?            &eostm -> add_block(e, b)
        | e=selfcall .b=block?           &eostm -> add_block(e, b)
        | e=call .b=block?               &eostm -> add_block(e, b)
        | e=expr     @"end of statement" &eostm -> e

   slots: a=slot as=(.",". slot)*               -> as.prepend(a)
    slot: "@" n=ident                           -> { type: #selfassign, name: n }
        | "\$" n=ident                          -> { type: #varassign, name: n }
        | "var"."\$" n=ident                    -> { type: #varcreate, name: n }
        | n=ident                               -> { type: #assign, name: n }

     stm: "\$" n=ident .o=op."=".. e=rhs        -> [{ type: #call, fn: o, args: [{ type: #varref, name: n}, e]}, { type: #varassign, name: n }]
        | "@"  n=ident .o=op."=".. e=rhs        -> [{ type: #call, fn: o, args: [{ type: #selfref, name: n}, e]}, { type: #selfassign, name: n }]
        | as=slots          ."=".. e=rhs        -> [e].cat(as)
        | e=rhs                                 -> [e]

    stms: t=stm ts=(.";".@"statement after ';'" stm)*     -> ts.prepend(t).reduce({l, r -> l.cat(r)})

   block: ":".. @"a block or function" (
           // this first one is needed, otherwise bodynl will produce paren at a very high indent
             "("..@"a closing ')'" b=body ..")"           -> b
           | function
           | "{"..@"a closing '}'" b=body .."}"           -> b
           | bodynl
           )

    body: ts=stms tss=(wsnl stms)* -> tss.prepend(ts).reduce({l, r -> l.cat(r)})

  bodynl: c=column ?(begin_body c) ts=stms
          tss=(wsnl cc=column ?(in_body c, cc) stms)* ?(end_body c) -> tss.prepend(ts).reduce({l, r -> l.cat(r)})

bodyempty: body
         | pass    -> []

   start: wsnl b=bodyempty wsnl end -> b
}

print inspect parse(io.File("test-input.tl").read)

